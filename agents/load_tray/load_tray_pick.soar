# Propose to pick up an object to put on the tray
sp {topstate*propose*pick-up
   (state <s> ^superstate nil
              ^agent-name load-tray
              ^svs.spatial-scene <sc>
              ^object-list <ol>
              ^done-loading <d>
             -^held-object)
   (<ol> ^first <oid>)
   (<d> -^object <oid>)
   (<sc> ^child.id <oid>)
-->
   (<s> ^operator <o> +)
   (<o> ^name pick-up
        ^use-objective <j>
        ^object-id <oid>)
   (<j> ^name waypoints
        ^target-object none
        ^direction min)
}

# Propose to clean up old commands once pick-up is over
sp {topstate*propose*clean-up-pick
   (state <s> ^superstate nil
              ^agent-name load-tray
              ^commands-present
              ^object-list <ol>
              ^done-loading <d>
              ^held-object <oid>)
   (<ol> ^first <oid>)
   (<d> -^object <oid>)
-->
   (<s> ^operator <o> +)
   (<o> ^name clean-up)
}

# Use this to also note that the pick-up is finished
sp {topstate*apply*clean-up*pickup-complete
   (state <s> ^superstate nil
              ^agent-name load-tray
              ^held-object <oid>
              ^operator.name clean-up)
-->
   (<s> ^pickup-complete <oid>)
}