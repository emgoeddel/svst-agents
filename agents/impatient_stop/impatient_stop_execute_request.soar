sp {execute-request*prefer*execute*over*cleanup
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs <v>
              ^operator <op1> +
              ^operator <op2> +)
   (<op1> ^name make-execute)
   (<op2> ^name cleanup-previous)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {execute-request*propose*make-execute
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs <svs>)
   (<svs> ^motor.trajectories <t>
          ^command <cmd>)
   (<cmd> ^find-trajectories.id <sid>
         -^execute-trajectory)
   (<t> ^set <st>)
   (<st> ^command-id <sid>
         ^trajectory <tr>)
   (<tr> ^id <tid>
         ^selected-by waypoints)
-->
   (<s> ^operator <o> +)
   (<o> ^name make-execute
        ^trajectory-set <sid>
        ^trajectory-id <tid>)
}

sp {execute-request*apply*make-execute
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs <v>
              ^operator <op>)
   (<op> ^name make-execute
         ^trajectory-set <sid>
         ^trajectory-id <tid>)
   (<v> ^command <c>)
-->
   (<c> ^execute-trajectory <x>)
   (<x> ^set-id <sid>
        ^trajectory-id <tid>)
}