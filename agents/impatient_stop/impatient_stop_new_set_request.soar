sp {set-request*propose*sample-xyz
   (state <s> ^superstate nil
              ^agent-name impatient-stop
             -^target)
   -->
   (<s> ^operator <o> +)
   (<o> ^name sample-xyz)
}

sp {set-request*apply*sample-xyz
   (state <s> ^superstate nil
              ^agent-name impatient-stop
             -^target
              ^operator.name sample-xyz)
   -->
   (<s> ^target <t>)
   (<t> ^center <c>
        ^frame self)
   (<c> ^x (+ 0.5 (rand-float 0.4))
        ^y (+ -1.0 (rand-float 2.0))
        ^z (+ 0.2 (rand-float 1.0)))
}

sp {set-request*propose*make-command
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> -^find-trajectories)
   -->
   (<s> ^operator <o> +)
   (<o> ^name make-command)
}

sp {set-request*apply*make-command
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>
              ^operator.name make-command)
   (<t> ^center <c>)
   (<c> ^x <xval>
        ^y <yval>
        ^z <zval>)
   -->
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^target <t>
         ^min-number 1
         ^max-time 30)
   (write (crlf) |Reach to x = | <xval> | y = | <yval> | z = | <zval>)
}

sp {set-request*propose*stop-command*success
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs <v>
              ^target <t>)
   (<v> ^command <cmd>
        ^motor.trajectories <traj>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> -^stop
          ^id <cid>)
   (<traj> ^set <set>)
   (<set> ^trajectory-count > 6
          ^command-id <cid>)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {set-request*propose*stop-command*planning-failure
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.planning-failure > 6
        -^stop)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {set-request*propose*stop-command*goal-failure
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.goal-invalid > 6
        -^stop)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {set-request*propose*stop-command*other-failure
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.other-error > 6
        -^stop)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {set-request*propose*stop-command*start-failure
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.start-invalid > 6
        -^stop)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {set-request*apply*stop-command
   (state <s> ^superstate nil
              ^agent-name impatient-stop
              ^svs.command <cmd>
              ^target <t>
              ^operator.name stop-command)
   (<cmd> ^find-trajectories <ft>)
   -->
   (<ft> ^stop now)
}