# Turn on SVS
svs --enable
svs ros.enable all

# Give the agent a name
sp {init-agent*propose*add-name
   (state <s> ^superstate nil
             -^agent-name)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-name)
}

sp {init-agent*apply*add-name
   (state <s> ^superstate nil
             -^agent-name
              ^operator.name add-name)
-->
   (<s> ^agent-name reach-test)
}

# Add the object properties data as an elaboration
sp {init-agent*elaborate*object-properties
   (state <s> ^superstate nil
              ^agent-name reach-test)
-->
   (<s> ^known-objects <ko>)
   (<ko> ^object <l>
         ^object <db>
         ^object <v>
         ^object <cb>)
   (<l> ^model-name demo_lamp
        ^property-name fragile)
   (<db> ^model-name demo_dropoff_box
         ^property-name user-target)
   (<v> ^model-name demo_vase
        ^property-name valuable)
   (<cb> ^model-name demo_cardboard_box
         ^property-name hittable)
}

# Add the objective map data as an elaboration
sp {init-agent*elaborate*property-objectives
   (state <s> ^superstate nil
              ^agent-name reach-test)
-->
   (<s> ^property-objective-map <pom>)
   (<pom> ^property <p1>
          ^property <p2>
          ^property <p3>
          ^property <p4>)
   (<p1> ^name fragile
         ^objective min-clear-subset
         ^direction max)
   (<p2> ^name user-target
         ^objective time-over
         ^direction min)
   (<p3> ^name valuable
         ^objective min-clearance
         ^direction max)
   (<p4> ^name hittable
         ^objective execution-time
         ^direction min)
}

# Decide on objective based on object
sp {init-agent*elaborate*selected-objective
   (state <s> ^superstate nil
              ^agent-name reach-test
              ^svs.spatial-scene <scn>
              ^known-objects <ko>
              ^property-objective-map <po>)
   (<scn> ^child.id <n>)
   (<ko> ^object <ob>)
   (<ob> ^model-name <n>
         ^property-name <pn>)
   (<po> ^property <p>)
   (<p> ^name <pn>
        ^objective <j>
        ^direction <d>)
-->
   (<s> ^selected-objective <so>)
   (<so> ^name <j>
         ^target-object <n>
         ^direction <d>)
}

# Or use no objective if it's the generic
sp {init-agent*elaborate*no-selected-objective
   (state <s> ^superstate nil
              ^agent-name reach-test
              ^svs.spatial-scene <scn>
              ^known-objects <ko>
              ^property-objective-map <po>)
   (<scn> ^child.id demo_generic)
-->
   (<s> ^selected-objective none)
}