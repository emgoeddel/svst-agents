sp {cleanup*propose*cleanup-previous*success
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^svs.command <cmd>)
   (<cmd> ^find-trajectories
          ^evaluate-trajectories
          ^execute-trajectory <et>)
   (<et> ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*propose*cleanup-previous*execute-failure
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^svs.command <cmd>)
   (<cmd> ^find-trajectories
          ^evaluate-trajectories
          ^execute-trajectory <et>)
   (<et> -^status running
         -^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*propose*cleanup-previous*planning-failure*no-trajectories
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^svs <svs>)
   (<svs> ^command <cmd>
          ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^status << stopped complete >>
         ^id <id>)
   (<t> ^set <set>)
   (<set> ^id <id>
          ^trajectory-count 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*propose*cleanup-previous*planning-failure*no-set
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^svs <svs>)
   (<svs> ^command <cmd>
          ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^status << stopped complete >>
         ^id <id>)
   (<t> -^set.command-id <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*apply*cleanup-previous*target
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^operator.name cleanup-previous
              ^target <t>)
-->
   (<s> ^target <t> -)
}

sp {cleanup*apply*cleanup-previous*find
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^operator.name cleanup-previous
              ^svs.command <cmd>)
   (<cmd> ^find-trajectories <ft>)
-->
   (<cmd> ^find-trajectories <ft> -)
}

sp {cleanup*apply*cleanup-previous*select-execute
   (state <s> ^superstate nil
              ^agent-name fixed-stop
              ^operator.name cleanup-previous
              ^svs.command <cmd>)
   (<cmd> ^evaluate-trajectories <st>
          ^execute-trajectory <et>)
-->
   (<cmd> ^evaluate-trajectories <st> -
          ^execute-trajectory <et> -)
}