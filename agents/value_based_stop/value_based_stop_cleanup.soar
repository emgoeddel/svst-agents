sp {cleanup*propose*cleanup-previous*success
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^svs.command <cmd>)
   (<cmd> ^find-trajectories
          ^evaluate-trajectories
          ^execute-trajectory <et>)
   (<et> ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*propose*cleanup-previous*execute-failure
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^svs.command <cmd>)
   (<cmd> ^find-trajectories
          ^evaluate-trajectories
          ^execute-trajectory <et>)
   (<et> -^status running
         -^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*propose*cleanup-previous*planning-failure
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^svs <svs>)
   (<svs> ^command <cmd>
          ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^status stopped
         ^failures.planning-failure > 10)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*propose*cleanup-previous*goal-failure
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^svs <svs>)
   (<svs> ^command <cmd>
          ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^status stopped
         ^failures.goal-invalid > 10)
-->
   (<s> ^operator <o> +)
   (<o> ^name cleanup-previous)
}

sp {cleanup*apply*cleanup-previous*target
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^operator.name cleanup-previous
              ^target <t>)
-->
   (<s> ^target <t> -)
}

sp {cleanup*apply*cleanup-previous*find
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^operator.name cleanup-previous
              ^svs.command <cmd>)
   (<cmd> ^find-trajectories <ft>)
-->
   (<cmd> ^find-trajectories <ft> -)
}

sp {cleanup*apply*cleanup-previous*eval
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^operator.name cleanup-previous
              ^svs.command <cmd>
              ^best-trajectory <bt>
              ^start-count <sc>
              ^dc-since-update <dcu>)
   (<cmd> ^evaluate-trajectories <et>)
-->
   (<s> ^best-trajectory <bt> -
        ^start-count <sc> -
        ^dc-since-update <dcu> -)
   (<cmd> ^evaluate-trajectories <et> -)
}

sp {cleanup*apply*cleanup-previous*execute
   (state <s> ^superstate nil
              ^agent-name value-based-stop
              ^operator.name cleanup-previous
              ^svs.command <cmd>)
   (<cmd> ^execute-trajectory <et>)
-->
   (<cmd> ^execute-trajectory <et> -)
}