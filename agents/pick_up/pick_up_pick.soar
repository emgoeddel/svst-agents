# Figure out which objects could be picked up
sp {topstate*propose*filter-objects
   (state <s> ^superstate nil
              ^agent-name pick-up
              ^svs.command <cmd>)
   (<cmd> -^extract_once.type graspable_select)
   -->
   (<s> ^operator <o> +)
   (<o> ^name filter-objects)
}

sp {topstate*apply*filter-objects
   (state <s> ^superstate nil
              ^agent-name pick-up
              ^svs.command <cmd>
              ^operator.name filter-objects)
   -->
   (<cmd> ^extract_once <eo>)
   (<eo> ^type graspable_select
         ^a <a>)
   (<a> ^type all_nodes)
}

# Propose to pick up a random graspable object
sp {topstate*propose*pick-up
   (state <s> ^superstate nil
              ^agent-name pick-up
              ^svs.command <cmd>)
   (<cmd> ^extract_once <eo>)
   (<eo> ^type graspable_select
         ^status success
         ^result <r>)
   (<r> ^record.value <id>)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name pick-up
        ^object-id <id>
        ^use-objective <j>)
   (<j> ^name occlusion-time
        ^target-object demo_generic
        ^direction min)
}