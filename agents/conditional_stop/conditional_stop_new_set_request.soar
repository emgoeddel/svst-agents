sp {set-request*propose*sample-xyz
   (state <s> ^superstate nil
              ^agent-name conditional-stop
             -^target)
   -->
   (<s> ^operator <o> +)
   (<o> ^name sample-xyz)
}

sp {set-request*apply*sample-xyz
   (state <s> ^superstate nil
              ^agent-name conditional-stop
             -^target
              ^operator.name sample-xyz)
   -->
   (<s> ^target <t>)
   (<t> ^center <c>)
   (<c> ^x (+ 0.5 (rand-float 0.4))
        ^y (+ -1.0 (rand-float 2.0))
        ^z (rand-float 1.0))
}

sp {set-request*propose*make-command
   (state <s> ^superstate nil
              ^agent-name conditional-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> -^find-trajectories)
   -->
   (<s> ^operator <o> +)
   (<o> ^name make-command)
}

sp {set-request*apply*make-command
   (state <s> ^superstate nil
              ^agent-name conditional-stop
              ^svs.command <cmd>
              ^target <t>
              ^operator.name make-command)
   -->
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^target <t>)
}

sp {set-request*propose*stop-command*success
   (state <s> ^superstate nil
              ^agent-name conditional-stop
              ^svs <v>
              ^target <t>)
   (<v> ^command <cmd>
        ^motor.trajectories <traj>)
   (<traj> ^set.trajectory <q>)
   (<cmd> ^find-trajectories)
   -->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {set-request*propose*stop-command*failure
   (state <s> ^superstate nil
              ^agent-name conditional-stop
              ^svs.command <cmd>
              ^target <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.planning-failure > 10)
   -->
   (<s> ^operator <o> +)
   (<o> ^name stop-command)
}

sp {set-request*apply*stop-command
   (state <s> ^superstate nil
              ^agent-name conditional-stop
              ^svs.command <cmd>
              ^target <t>
              ^operator.name stop-command)
   (<cmd> ^find-trajectories <ft>)
   -->
   (<ft> ^stop now)
}