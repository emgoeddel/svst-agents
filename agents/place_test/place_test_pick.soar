# Propose to pick up the object to put on the tray
sp {topstate*propose*pick-up*not-box
   (state <s> ^superstate nil
              ^agent-name place-test
              ^load-object <lid> << can_coke can_sprite can_fanta cup_blue >>
             -^held-object
              ^pick-objectives <po>)
-->
   (<s> ^operator <o> +)
   (<o> ^name pick-up
        ^objectives <po>
        ^object-id <lid>)
}

sp {topstate*propose*pick-up*box
   (state <s> ^superstate nil
              ^agent-name place-test
              ^load-object <lid> << tea_box_generic tea_box_lamp >>
             -^held-object
              ^pick-objectives <po>)
-->
   (<s> ^operator <o> +)
   (<o> ^name pick-up
        ^objectives <po>
        ^object-id tea_box)
}

# Propose to clean up old commands once pick-up is over
sp {topstate*propose*clean-up-pick
   (state <s> ^superstate nil
              ^agent-name place-test
              ^commands-present
              ^held-object <oid> { <oid> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name clean-up)
}

# Use this to also note that the pick-up is finished
sp {topstate*apply*clean-up*pickup-complete
   (state <s> ^superstate nil
              ^agent-name place-test
              ^held-object <oid> { <oid> <> none }
              ^operator.name clean-up)
-->
   (<s> ^pickup-complete <oid>)
}

# Or halt if it failed
sp {topstate*halt*pickup-failure
   (state <s> ^superstate nil
              ^agent-name place-test
              ^commands-present
              ^held-object none)
-->
   (halt)
}