# If the superstate has impassed on put-down, start the put-down process

############## TARGET ################
sp {put-down*propose*get-put-down-target
   (state <s> ^name put-down
              ^target-object
             -^target-area
              ^topstate-svs.command <cmd>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-put-down-target)
}

sp {put-down*apply*get-put-down-target*orientation
   (state <s> ^name put-down
              ^held-object <h>
              ^target-object <t>
              ^topstate-svs.command <cmd>
              ^operator.name get-put-down-target)
-->
   (<cmd> ^extract_once <eo>)
   (<eo> ^type grasp_first
         ^a <a>
         ^frame self)
   (<a> ^type node
        ^id <h>)
}

sp {put-down*apply*get-put-down-target*area
   (state <s> ^name put-down
              ^held-object <h>
              ^target-object <t>
              ^topstate-svs.command <cmd>
              ^operator.name get-put-down-target)
-->
   (<cmd> ^extract_once <eo>)
   (<eo> ^type put_down_area
         ^holding <hn>
         ^target <tn>
         ^frame self)
   (<hn> ^type node
         ^id <h>)
   (<tn> ^type node
         ^id <t>)
}

############## FIND TRAJECTORIES ################
sp {put-down*propose*make-find-trajectories
   (state <s> ^name put-down
              ^topstate-svs.command <cmd>
              ^target-area)
   (<cmd> -^find-trajectories)
-->
   (<s> ^operator <o> +)
   (<o> ^name make-find-trajectories)
}

sp {put-down*apply*make-find-trajectories*mast
   (state <s> ^name put-down
              ^topstate-svs.command <cmd>
              ^target-area <ta>
              ^held-object <oid>
             -^objective none
              ^operator.name make-find-trajectories)
   (<ta> ^center <c>
         ^dimensions <d>
         ^gripper-orientation <go>)
   (<go> ^roll <gro>
         ^pitch <gpi>
         ^yaw <gya>)
-->
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^target <pt>
         ^holding-object <oid>
         ^max-time 10
         ^max-number 30)
   (<pt> ^center <c>
         ^size <d>
         ^orientation <or>
         ^frame self)
   (<or> ^x <gro>
         ^y <gpi>
         ^z <gya>)
}

sp {put-down*apply*make-find-trajectories*no-mast
   (state <s> ^name put-down
              ^topstate-svs.command <cmd>
              ^planning-target <pt>
              ^held-object <oid>
              ^objective none
              ^operator.name make-find-trajectories)
-->
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^target <pt>
         ^holding-object <oid>
         ^max-number 3)
}

############## SELECT TRAJECTORY ################

# Let the agent stop a command if needed before doing selection
sp {put-down*prefer*stop-over-select
   (state <s> ^name put-down
              ^operator <op1> +
              ^operator <op2> +)
   (<op1> ^name stop-command)
   (<op2> ^name make-select)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {put-down*propose*make-select
   (state <s> ^name put-down
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^id <sid>
         ^status << stopped complete >> )
   (<t> ^set <st>)
   (<st> ^command-id <sid>
        -^trajectory.selected-by <any>)
-->
   (<s> ^operator <o> +)
   (<o> ^name make-select
        ^trajectory-set <sid>)
}

sp {put-down*apply*make-select
   (state <s> ^name put-down
              ^objective <obj> <> none
              ^topstate-svs <v>
              ^operator <op>)
   (<op> ^name make-select
         ^trajectory-set <sid>)
   (<obj> ^name <on> <> centrality
          ^target-object <to>
          ^direction <dir>)
   (<v> ^command <c>)
-->
   (<c> ^evaluate-trajectories <eval>)
   (<eval> ^set-id <sid>
           ^type select
           ^objective <on>
           ^obstacle <to>
           ^direction <dir>
           ^number 1)
}

sp {put-down*apply*make-select*centrality
   (state <s> ^name put-down
              ^objective <obj>
              ^topstate-svs <v>
              ^target-area <ta>
              ^operator <op>)
   (<op> ^name make-select
         ^trajectory-set <sid>)
   (<obj> ^name centrality
          ^direction <dir>)
   (<ta> ^center <tc>
         ^dimensions <td>)
   (<v> ^command <c>)
-->
   (<c> ^evaluate-trajectories <eval>)
   (<eval> ^set-id <sid>
           ^type select
           ^objective centrality
           ^area-center <tc>
           ^area-dimensions <td>
           ^direction <dir>
           ^number 1)
}

# This is only so that the evaluation function is called in SVS!
sp {put-down*apply*make-select*no-mast
   (state <s> ^name put-down
              ^objective none
              ^topstate-svs <v>
              ^operator <op>)
   (<op> ^name make-select
         ^trajectory-set <sid>)
   (<v> ^command <c>)
-->
   (<c> ^evaluate-trajectories <eval>)
   (<eval> ^set-id <sid>
           ^type select
           ^objective waypoints
           ^number 1)
}

############## EXEC TRAJECTORY ################
sp {put-down*propose*make-execute
   (state <s> ^name put-down
             -^objective none
              ^topstate-svs <svs>)
   (<svs> ^motor.trajectories <t>
          ^command <cmd>)
   (<cmd> ^find-trajectories.id <sid>
         -^execute-trajectory)
   (<t> ^set <st>)
   (<st> ^command-id <sid>
         ^trajectory <tr>)
   (<tr> ^id <tid>
         ^selected-by <any>)
-->
   (<s> ^operator <o> +)
   (<o> ^name make-execute
        ^trajectory-set <sid>
        ^trajectory-id <tid>)
}

sp {put-down*propose*make-execute*no-mast
   (state <s> ^name put-down
              ^objective none
              ^topstate-svs <svs>)
   (<svs> ^motor.trajectories <t>
          ^command <cmd>)
   (<cmd> ^find-trajectories.id <sid>
         -^execute-trajectory)
   (<t> ^set <st>)
   (<st> ^command-id <sid>
         ^trajectory <tr>)
   (<tr> ^selected-by waypoints) #Selection not used in this case!
-->
   (<s> ^operator <o> +)
   (<o> ^name make-execute
        ^trajectory-set <sid>
        ^trajectory-id 0)
}

sp {put-down*apply*make-execute
   (state <s> ^name put-down
              ^topstate-svs <v>
              ^operator <op>)
   (<op> ^name make-execute
         ^trajectory-set <sid>
         ^trajectory-id <tid>)
   (<v> ^command <c>)
-->
   (<c> ^execute-trajectory <x>)
   (<x> ^set-id <sid>
        ^trajectory-id <tid>)
}

############## DROP POSE ################

sp {put-down*propose*get-drop-pose
   (state <s> ^name put-down
              ^target-object
             -^drop-movement
              ^topstate-svs.command <cmd>)
   (<cmd> ^execute-trajectory <x>)
   (<x> ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-drop-pose)
}

# hard-coded target for initial tray test!
sp {put-down*apply*get-drop-pose
   (state <s> ^name put-down
              ^target-object <id>
              ^topstate-svs.command <cmd>
              ^operator.name get-drop-pose)
-->
   (<s> ^drop-movement <t>)
   (<t> ^x 0.0
        ^y 0.0
        ^z -0.05)
}

############## STRAIGHT LINE IN ################

sp {put-down*propose*do-straight-line-in
   (state <s> ^name put-down
              ^drop-movement
              ^topstate-svs.command <cmd>)
   (<cmd> -^straight-line-motion
          -^set-gripper)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-straight-line-in)
}

sp {put-down*apply*do-straight-line-in
   (state <s> ^name put-down
              ^drop-movement <t>
              ^topstate-svs.command <cmd>
              ^operator.name do-straight-line-in)
   (<t> ^x <px>
        ^y <py>
        ^z <pz>)
-->
   (<cmd> ^straight-line-motion <sl> )
   (<sl> ^target <xyz>
         ^type relative)
   (<xyz> ^x <px>
          ^y <py>
          ^z <pz>)
}

############## OPEN GRIPPER ################
sp {put-down*propose*open-gripper
   (state <s> ^name put-down
              ^topstate-svs.command <cmd>)
   (<cmd> ^straight-line-motion <sli>
         -^set-gripper)
   (<sli> ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name open-gripper)
}

sp {put-down*apply*open-gripper
   (state <s> ^name put-down
              ^topstate-svs.command <cmd>
              ^operator.name open-gripper)
   (<cmd> ^straight-line-motion <sli>)
-->
   (<cmd> ^set-gripper <sg>
          ^straight-line-motion <sli> -)
   (<sg> ^target 0.10) #open
}

############## STRAIGHT LINE OUT ################
sp {put-down*propose*do-straight-line-out
   (state <s> ^name put-down
              ^drop-movement
              ^topstate-svs.command <cmd>)
   (<cmd> ^set-gripper <cg>
         -^straight-line-motion)
   (<cg> ^target 0.10 #open
         ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-straight-line-out)
}

sp {put-down*apply*do-straight-line-out
   (state <s> ^name put-down
              ^drop-movement <dm>
              ^topstate-svs.command <cmd>
              ^operator.name do-straight-line-out)
   (<dm> ^x <px>
         ^y <py>
         ^z <pz>)
-->
   (<cmd> ^straight-line-motion <sl> )
   (<sl> ^target <xyz>
         ^type relative)
   (<xyz> ^x <px>
          ^y <py>
          ^z (+ 0.02 (- <pz>)))
}

############## CLOSE GRIPPER ################
sp {put-down*propose*close-gripper
   (state <s> ^name put-down
             -^finished-moving
              ^topstate-svs.command <cmd>)
   (<cmd> ^straight-line-motion <sl>
          ^set-gripper <og>
         -^set-gripper.target 0.0)
   (<sl> ^status finished)
   (<og> ^target 0.10
         ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name close-gripper)
}

sp {put-down*apply*close-gripper
   (state <s> ^name put-down
              ^topstate-svs.command <cmd>
              ^operator.name close-gripper)
-->
   (<cmd> ^set-gripper <sg> )
   (<sg> ^target 0.0) #closed
}

############## UPDATE HELD ################
sp {put-down*propose*update-held-object
   (state <s> ^name put-down
              ^held-object <oid>
              ^finished-moving)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-held-object
        ^possible-object <oid>)
}
