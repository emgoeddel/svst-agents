# If the superstate has impassed on pick-up, start the pick-up process

############## GRASP FIRST ################
sp {pick-up*propose*get-grasp-target
   (state <s> ^name pick-up
              ^target-object
              ^topstate-svs.command <cmd>)
   (<cmd> -^extract_once.type grasp_first)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-grasp-target)
}

sp {pick-up*apply*get-grasp-target
   (state <s> ^name pick-up
              ^target-object <id>
              ^topstate-svs.command <cmd>
              ^operator.name get-grasp-target)
-->
   (<cmd> ^extract_once <eo> )
   (<eo> ^type grasp_first
         ^a <a>
         ^frame self)
   (<a> ^type node
        ^id <id>)
}

############## FIND TRAJECTORIES ################
sp {pick-up*propose*make-find-trajectories
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>
              ^target-pose <t>)
   (<cmd> -^find-trajectories)
-->
   (<s> ^operator <o> +)
   (<o> ^name make-find-trajectories)
}

sp {pick-up*apply*make-find-trajectories*mast
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>
              ^target-pose <tp>
             -^objectives none
              ^operator.name make-find-trajectories)
   (<tp> ^x <px>
         ^y <py>
         ^z <pz>
         ^roll <rx>
         ^pitch <ry>
         ^yaw <rz>)
-->
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^target <t>
         ^max-time 10
         ^max-number 30)
   (<t> ^center <c>
        ^orientation <or>
        ^frame self)
   (<c> ^x <px>
        ^y <py>
        ^z <pz>)
   (<or> ^x <rx>
         ^y <ry>
         ^z <rz>)
}

sp {pick-up*apply*make-find-trajectories*no-mast
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>
              ^target-pose <tp>
              ^objectives none
              ^operator.name make-find-trajectories)
   (<tp> ^x <px>
         ^y <py>
         ^z <pz>
         ^roll <rx>
         ^pitch <ry>
         ^yaw <rz>)
-->
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^target <t>
         ^max-number 3)
   (<t> ^center <c>
        ^orientation <or>
        ^frame self)
   (<c> ^x <px>
        ^y <py>
        ^z <pz>)
   (<or> ^x <rx>
         ^y <ry>
         ^z <rz>)
}

############## SELECT TRAJECTORY ################

# Let the agent stop a command if needed before doing selection
sp {pick-up*prefer*stop-over-select
   (state <s> ^name pick-up
              ^operator <op1> +
              ^operator <op2> +)
   (<op1> ^name stop-command)
   (<op2> ^name do-selection)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {pick-up*propose*do-selection
   (state <s> ^name pick-up
             -^selection-result.set-id <sid>
              ^objectives { <objs> <> none }
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^id <sid>
         ^status << stopped complete >> )
-->
   (<s> ^operator <o> +)
   (<o> ^name do-selection
        ^trajectory-set <sid>
        ^objectives <objs>)
}

# To get output for the reach test, calls selection for waypoints
# BUT DOES NOT USE THE RESULT
sp {pick-up*propose*do-selection*no-mast
   (state <s> ^name pick-up
              ^superstate.agent-name reach-test
             -^selection-result.set-id <sid>
              ^objectives none
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^id <sid>
         ^status << stopped complete >> )
-->
   (<s> ^operator <o> +)
   (<o> ^name do-selection
        ^trajectory-set <sid>
        ^objectives <objs>)
   (<objs> ^first <fo>)
   (<fo> ^name waypoints
         ^parameters <pms>)
   (<pms> ^direction min
          ^obstacle demo_generic)
}

############## EXEC TRAJECTORY ################
sp {pick-up*propose*make-execute
   (state <s> ^name pick-up
              ^selection-result <sr>
             -^objectives none
              ^topstate-svs.command <cmd>)
   (<cmd> ^find-trajectories.id <sid>
         -^execute-trajectory)
   (<sr> ^trajectory-id <tid>
         ^set-id <sid>)
-->
   (<s> ^operator <o> +)
   (<o> ^name make-execute
        ^trajectory-set <sid>
        ^trajectory-id <tid>)
}

sp {pick-up*propose*make-execute*skip-selection*no-mast
   (state <s> ^name pick-up
             -^superstate.agent-name reach-test
             -^selection-result.set-id <sid>
              ^objectives none
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories <t>)
   (<cmd> ^find-trajectories <ft>
         -^execute-trajectory)
   (<ft> ^id <sid>
         ^status << stopped complete >> )
-->
   (<s> ^operator <o> + =)
   (<o> ^name make-execute
        ^trajectory-set <sid>
        ^trajectory-id 0) # NO SELECTION, just uses first trajectory

}

sp {pick-up*propose*make-execute*no-mast
   (state <s> ^name pick-up
              ^superstate.agent-name reach-test
              ^selection-result <sr>
              ^objectives none
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories.set <st>)
   (<cmd> ^find-trajectories.id <sid>
         -^execute-trajectory)
   (<st> ^command-id <sid>
         ^trajectory.id <tid>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name make-execute
        ^trajectory-set <sid>
        ^trajectory-id 0) # NO SELECTION, just uses first trajectory
}

sp {pick-up*apply*make-execute
   (state <s> ^name pick-up
              ^topstate-svs <v>
              ^operator <op>)
   (<op> ^name make-execute
         ^trajectory-set <sid>
         ^trajectory-id <tid>)
   (<v> ^command <c>)
-->
   (<c> ^execute-trajectory <x>)
   (<x> ^set-id <sid>
        ^trajectory-id <tid>)
}

# Comment out to have the reaching agent actually execute!
sp {pick-up*reach-test-only*halt
   (state <s> ^name pick-up
              ^superstate <ss>
              ^topstate-svs.command <cmd>)
   (<ss> ^agent-name reach-test)
   (<cmd> ^execute-trajectory <x>)
   (<x> ^status running)
-->
   (halt)
}


############## GRASP SECOND ################

sp {pick-up*propose*get-final-grasp
   (state <s> ^name pick-up
              ^target-object
              ^topstate-svs.command <cmd>)
   (<cmd> ^execute-trajectory <x>
         -^extract_once.type grasp_second)
   (<x> ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-final-grasp)
}

sp {pick-up*apply*get-final-grasp
   (state <s> ^name pick-up
              ^target-object <id>
              ^topstate-svs.command <cmd>
              ^operator.name get-final-grasp)
-->
   (<cmd> ^extract_once <eo> )
   (<eo> ^type grasp_second
         ^a <a>
         ^frame self)
   (<a> ^type node
        ^id <id>)
}

############## OPEN GRIPPER ################
sp {pick-up*propose*open-gripper
   (state <s> ^name pick-up
              ^final-pose
              ^topstate-svs.command <cmd>)
   (<cmd> -^set-gripper)
-->
   (<s> ^operator <o> +)
   (<o> ^name open-gripper)
}

sp {pick-up*apply*open-gripper
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>
              ^operator.name open-gripper)
-->
   (<cmd> ^set-gripper <sg> )
   (<sg> ^target 0.10) #open
}

############## STRAIGHT LINE IN ################

sp {pick-up*propose*do-straight-line-in
   (state <s> ^name pick-up
              ^final-pose
              ^topstate-svs.command <cmd>)
   (<cmd> ^set-gripper <sg>
         -^straight-line-motion
         -^set-gripper.target 0.0)
   (<sg> ^target 0.10 #open
         ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-straight-line-in)
}

sp {pick-up*apply*do-straight-line-in
   (state <s> ^name pick-up
              ^final-pose <p>
              ^topstate-svs.command <cmd>
              ^operator.name do-straight-line-in)
   (<p> ^x <px>
        ^y <py>
        ^z <pz>)
-->
   (<cmd> ^straight-line-motion <sl> )
   (<sl> ^target <xyz>)
   (<xyz> ^x <px>
          ^y <py>
          ^z <pz>)
}

############## CLOSE GRIPPER ################
sp {pick-up*propose*close-gripper
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>)
   (<cmd> ^straight-line-motion <sl>
         -^set-gripper.target 0.0)
   (<sl> ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name close-gripper)
}

sp {pick-up*apply*close-gripper
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>
              ^operator.name close-gripper)
   (<cmd> ^straight-line-motion <sli>)
-->
   (<cmd> ^set-gripper <sg>
          ^straight-line-motion <sli> -)
   (<sg> ^target 0.0) #closed
}

############## STRAIGHT LINE OUT ################
sp {pick-up*propose*do-straight-line-out
   (state <s> ^name pick-up
              ^target-pose
             -^finished-moving
              ^topstate-svs.command <cmd>)
   (<cmd> ^set-gripper <cg>)
   (<cg> ^target 0.0 #closed
         ^status finished)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-straight-line-out)
}

sp {pick-up*apply*do-straight-line-out
   (state <s> ^name pick-up
              ^target-pose <p>
              ^topstate-svs.command <cmd>
              ^operator.name do-straight-line-out)
   (<p> ^x <px>
        ^y <py>
        ^z <pz>)
-->
   (<cmd> ^straight-line-motion <sl> )
   (<sl> ^target <xyz>)
   (<xyz> ^x <px>
          ^y <py>
          ^z <pz>)
}

############## CHECK SUCCESS ################
sp {pick-up*propose*update-held-object
   (state <s> ^name pick-up
              ^target-object <oid>
              ^target-pose
              ^finished-moving)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-held-object
        ^possible-object <oid>)
}
