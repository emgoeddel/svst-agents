sp {errors*propose*stop-command*planning-failure
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.planning-failure > 10
        -^stop)
-->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {errors*propose*stop-command*goal-failure
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.goal-invalid > 10
        -^stop)
-->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {errors*propose*stop-command*other-failure
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.other-error > 10
        -^stop)
-->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {errors*propose*stop-command*start-failure
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^failures.start-invalid > 10
        -^stop)
-->
   (<s> ^operator <o> + =)
   (<o> ^name stop-command)
}

sp {errors*apply*stop-command
   (state <s> ^name pick-up
              ^topstate-svs.command <cmd>
              ^operator.name stop-command)
   (<cmd> ^find-trajectories <ft>)
-->
   (<ft> ^stop now)
}

sp {errors*propose*interrupt-on-error*empty-set
   (state <s> ^name pick-up
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories <mt>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^stop <p>
         ^id <sid>
         ^status stopped)
   (<mt> ^set <ts>)
   (<ts> ^command-id <sid>
         ^trajectory-count 0)
-->
   (<s> ^operator <o>)
   (<o> ^name interrupt-on-error)
}

sp {errors*propose*interrupt-on-error*no-set
   (state <s> ^name pick-up
              ^topstate-svs <v>)
   (<v> ^command <cmd>
        ^motor.trajectories <mt>)
   (<cmd> ^find-trajectories <ft>)
   (<ft> ^stop <p>
         ^id <sid>
         ^status stopped)
   (<mt> -^set.command-id <sid>)
-->
   (<s> ^operator <o>)
   (<o> ^name interrupt-on-error)
}

sp {errors*apply*interrupt-on-error
   (state <s> ^name pick-up
              ^operator.name interrupt-on-error)
-->
   (interrupt)
}