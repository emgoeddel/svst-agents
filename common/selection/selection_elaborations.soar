# Copy superstate operator info to substate
sp {selection*elaborate*state
   (state <s> ^superstate <ss>
              ^impasse no-change)
   (<ss> ^operator <o>)
   (<o> ^name do-selection
        ^trajectory-set <sid>
        ^objectives <obs>)
-->
   (<s> ^name selection
        ^objectives <obs>
        ^trajectory-set <sid>)
}

# Use topstate SVS
sp {selection*elaborate*svs
   (state <s> ^name selection)
   (state <ts> ^superstate nil
               ^svs <v>)
-->
   (<s> ^topstate-svs <v>)
}


# Once the final command has processed, make result more visible
sp {selection*apply*elaborate-result
   (state <s> ^name << pick-up put-down >>
              ^topstate-svs <svs>)
   (<svs> ^command <c>
          ^motor.trajectories <t>)
   (<c> ^evaluate-trajectories <eval>)
   (<eval> ^set-id <sid>
           ^type select
           ^objective { <on> <> endpoint-distance }
           ^number 1
           ^status success)
   (<t> ^set <tset>)
   (<tset> ^command-id <sid>
           ^trajectory <sel>)
   (<sel> ^id <tid>
          ^selected-by <on> )
-->
   (<s> ^selection-result <r>)
   (<r> ^set-id <sid>
        ^trajectory-id <tid>)
}

# Hack for using endpoint-distance twice (ie two different objects)
sp {selection*apply*elaborate-result*endpoint-distance*once
   (state <s> ^name << pick-up put-down >>
              ^topstate-svs <svs>)
   (<svs> ^command <c>
          ^motor.trajectories <t>)
   (<c> ^evaluate-trajectories <eval>)
   (<eval> ^set-id <sid>
           ^type select
           ^objective endpoint-distance
           ^number 1
           ^status success)
   (<t> ^set <tset>)
   (<tset> ^command-id <sid>
           ^trajectory <sel>
          -^trajectory.selected-by endpoint-distance-d)
   (<sel> ^id <tid>
          ^selected-by endpoint-distance)
-->
   (<s> ^selection-result <r>)
   (<r> ^set-id <sid>
        ^trajectory-id <tid>)
}

sp {selection*apply*elaborate-result*endpoint-distance*duplicate
   (state <s> ^name << pick-up put-down >>
              ^topstate-svs <svs>)
   (<svs> ^command <c>
          ^motor.trajectories <t>)
   (<c> ^evaluate-trajectories <eval>)
   (<eval> ^set-id <sid>
           ^type select
           ^objective endpoint-distance
           ^number 1
           ^status success)
   (<t> ^set <tset>)
   (<tset> ^command-id <sid>
           ^trajectory <sel>)
   (<sel> ^id <tid>
          ^selected-by endpoint-distance-d)
-->
   (<s> ^selection-result <r>)
   (<r> ^set-id <sid>
        ^trajectory-id <tid>)
}